plugins {
    id "architectury-plugin" version "${arch_plugin_version}"
    id "dev.architectury.loom" version "${loom_version}" apply false
	id "io.github.juuxel.loom-vineflower" version "${vineflower_version}" apply false
	id "io.github.p03w.machete" version "${machete_version}"
    id "io.github.pacifistmc.forgix" version "${forgix_version}"
}

// These are the access wideners depending on the minecraft version
def mcVersionToAccessWidenerVersion = [
        "1.19.2": "1_19_2",
        "1.20.1": "1_20_1",
]
rootProject.ext.set("access_wideners", mcVersionToAccessWidenerVersion)

loadProperties()
makeAccessWidener()
defineMixins()

forgix {
    group = rootProject.maven_group
    mergedJarName = "${rootProject.archives_base_name}-${rootProject.mod_version}${getBuildNumber()}+mc${rootProject.minecraft_version}.jar"
	outputDir = "build/libs/merged"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects { p ->
    apply plugin: "dev.architectury.loom"
	apply plugin: "io.github.juuxel.loom-vineflower"

	def froge = p == project(":forge")
	def fabric = p == project(":fabric")
	def common = p == project(":common")

	def awFile = project(":common").file("src/main/resources/calamity.accesswidener")

    loom {
		silentMojangMappingsLicense()

		if(awFile.exists()) {
			accessWidenerPath = awFile

			if(froge) {
				forge {
					convertAccessWideners = true
					extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
				}
			}
		}
	}

    configurations {
		addJar
		shade

        addJarCompile
		addJarRuntime

		if (!common) {
			include.extendsFrom addJar
		}

        compileOnly.extendsFrom addJarCompile
		annotationProcessor.extendsFrom addJarCompile
        implementation.extendsFrom addJarRuntime

		addJar.extendsFrom addJarCompile
		addJar.extendsFrom addJarRuntime

		implementation.extendsFrom shade
    }

	repositories {
		mavenCentral()
		maven { url = "https://maven.parchmentmc.org" }
		maven { url = "https://maven.quiltmc.org/repository/release" }
		maven {
			url = "https://maven.tterrag.com/"
			content { includeGroup("com.jozufozu.flywheel") }
		}
	}

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings(loom.layered {
			it.mappings("org.quiltmc:quilt-mappings:${rootProject.minecraft_version}+build.${rootProject.quilt}:intermediary-v2")
			it.parchment("org.parchmentmc.data:parchment-${rootProject.minecraft_version}:${rootProject.parchment}@zip")
			it.officialMojangMappings { nameSyntheticMembers = false }
		})

        addJarCompile "systems.manifold:manifold-preprocessor:${rootProject.manifold_version}"
		addJarCompile "systems.manifold:manifold-ext:${rootProject.manifold_version}"
		addJarRuntime "systems.manifold:manifold-ext-rt:${rootProject.manifold_version}"
        addJarRuntime "systems.manifold:manifold-rt:${rootProject.manifold_version}"
        addJar "systems.manifold:manifold-util:${rootProject.manifold_version}"

        // Dev Env Optimizations
        //// https://www.cursemaven.com/
        //// https://docs.modrinth.com/docs/tutorials/maven/

        if (fabric) {
            if (rootProject.hasProperty("lazydfu")) {
				modRuntimeOnly "maven.modrinth:lazydfu:${rootProject.lazydfu}"
            }
            if (rootProject.hasProperty("smoothboot_fabric")) {
				modRuntimeOnly "curse.maven:smoothboot-fabric-415758:${rootProject.smoothboot_fabric}"
            }
        }

        if (froge) {
            if (rootProject.hasProperty("bmb")) {
				modRuntimeOnly "curse.maven:better-mods-button-541584:${rootProject.bmb}"
            }
            if (rootProject.hasProperty("smoothboot_forge")) {
				modRuntimeOnly "curse.maven:smoothboot-forge-633412:${rootProject.smoothboot_forge}"
            }
            if (rootProject.hasProperty("catalogue")) {
				modRuntimeOnly "curse.maven:catalogue-459701:${rootProject.catalogue}"
			}
			if (rootProject.hasProperty("ferritecore")) {
				modRuntimeOnly "curse.maven:ferritecore-429235:${rootProject.ferritecore}"
			}
        }
    }

    processResources {
        def properties = [
			version: rootProject.mod_version,
			minecraft_version: rootProject.minecraft_version,
			forge: rootProject.forge.split("\\.")[0],
			fabric: rootProject.fabric,
			fabric_api: rootProject.fabric_api,
			create_forge: rootProject.create_forge.split("-")[0],
			create_fabric: rootProject.create_fabric,
        ]

        inputs.properties properties
		inputs.property("aw", rootProject.access_widener)

        filesMatching("architectury.common.json") {
            expand(aw: rootProject.access_widener)
        }

        filesMatching("fabric.mod.json") {
            expand properties
        }

        filesMatching("META-INF/mods.toml") {
            expand properties
        }
    }

    remapJar.finalizedBy(mergeJars)
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' } // manifold
        maven { url 'https://jitpack.io' } // idk a lot of stuff
        maven { // modrinth
            url "https://api.modrinth.com/maven"
            content { includeGroup "maven.modrinth" }
        }
        maven { // curseforge
            url "https://www.cursemaven.com"
            content { includeGroup "curse.maven" }
        }
    }

	tasks.withType(JavaCompile).configureEach {
		options.encoding = "UTF-8"
		options.release = 17
		options.compilerArgs += ['-Xplugin:Manifold']
	}

	java {
        withSourcesJar()
    }

	machete {
		// disable machete locally for faster builds
		enabled = System.getenv("GITHUB_RUN_NUMBER") != null
	}
}

static def getBuildNumber() {
	String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
	return (buildNumber != null ? ".${buildNumber}" : "")
}


/* Cursed stuff is defined below! Beware of dog */



// Defines the preprocessors
def setupManifoldProcessors(List<String> mcVers, int mcIndex) {
    def redefineList = new ArrayList<String>()
    for (int i = 0; i < mcVers.size(); i++) {
        String mcStr = "MC_" + mcVers.get(i).replace(".", "_")

        if (mcIndex < i)
            redefineList.add("PRE_$mcStr")

        if (mcIndex <= i)
            redefineList.add("PRE_CURRENT_$mcStr")

        if (mcIndex == i)
            redefineList.add(mcStr)

        if (mcIndex > i)
            redefineList.add("POST_$mcStr")

        if (mcIndex >= i)
            redefineList.add("POST_CURRENT_$mcStr")
    }

    StringBuilder sb = new StringBuilder()
		.append("# DO NOT EDIT\n")
		.append("# Generated by the build script. Change mcVer property to change minecraft version.\n\n")

    for (String redefinedVersion : redefineList) {
        sb.append(redefinedVersion).append("=\n")
    }

    new File(projectDir, "build.properties").text = sb.toString()
}

// Loads the properties for each version & sets up the preprocessors
def loadProperties() {
    def mcVersion = ""
    def mcVers = mc_versions.split(", ")
    int mcIndex = -1

    println "Avalible Minecraft Versions: ${mc_versions}"

    if (project.hasProperty("mcVer")) {
        mcVersion = mcVer
        mcIndex = Arrays.asList(mcVers).indexOf(mcVer)
    }

    if (mcIndex == -1) {
        println "Defaulting to ${rootProject.default_mc_version} since the set mcVer is invalid."
        println "Use -PmcVer='${rootProject.default_mc_version}' or edit gradle.properties to set the minecraft version."

        mcVersion = default_mc_version
        mcIndex = Arrays.asList(mcVers).indexOf(default_mc_version)
    }

    println "Using " + mcVersion + ".properties."
    def properties = new Properties()

    properties.load(new FileInputStream("$rootProject.rootDir/"+"$mcVersion"+".properties"))

    properties.each { property ->
        rootProject.ext.set(property.key, property.value)
    }

    setupManifoldProcessors(Arrays.asList(mcVers), mcIndex)

    rootProject.ext.set("access_widener_version", rootProject.access_wideners.get(mcVersion))
}

// Sets up the accesswideners
def makeAccessWidener() {
    def accessWidenerFile = project(":common").file("src/main/resources/calamity.accesswidener")
    def versionAccessWidenerFile = project(":common").file("src/main/resources/${rootProject.access_widener_version}.calamity.aw")

    if (accessWidenerFile.exists()) {
        delete accessWidenerFile
    }

    if (versionAccessWidenerFile.exists()) {
        copy {
            from versionAccessWidenerFile
            into project(":common").file("src/main/resources/")
            rename "${rootProject.access_widener_version}.calamity.aw", "calamity.accesswidener"
        }
        def text = accessWidenerFile.text
        text += "\n# DO NOT EDIT\n# Generated by the build script. Edit ${versionAccessWidenerFile.name} instead!"

        accessWidenerFile.text = text
    } else {
        versionAccessWidenerFile.createNewFile()
        versionAccessWidenerFile.text = "accessWidener   v2  named"
    }
    rootProject.ext.set("access_widener", "${rootProject.access_widener_version}.calamity.aw")
}

// Defines the mixins
def defineMixins() {
    def mixins = new ArrayList<String>()
    def projects = new ArrayList<String>()

    for (Project proj : rootProject.subprojects) {
        projects.add(proj.name)
    }

    for (String proj : projects) {
        for (File file : project(proj).file("src/main/resources/").listFiles()) {
            if (file.getName().endsWith("mixin.json") || file.getName().endsWith("mixins.json")) {
                mixins.add(file.getName())
            }
        }
    }

    rootProject.ext.set("mixins", mixins)
}
